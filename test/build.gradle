apply plugin: 'android-sdk-manager'
apply plugin: AndroidStubPlugin
apply plugin: 'java'

def Properties props = new Properties()
props.load(new FileInputStream(file('../local.properties')))

def android = [
		sdk   : props["sdk.dir"],
		target: 'android-19'
]

repositories {
	mavenCentral()
	maven {
		url "${android.sdk}/extras/android/m2repository/"
	}
}

dependencies {
	compile project(':core')
	compile project(':compiler')
}

tasks.withType(Test) {
	scanForTestClasses = false
	include "**/*Test.class"
}

// This plugin's only purpose is to provide the android-sdk-manager plugin the properties it expects from the android
// plugin. This is necessary because this is a Java project reliant on the Android library, and not an Android project.
class AndroidStubPlugin implements Plugin<Project> {
	void apply(Project project) {
		project.extensions.create('android', AndroidStubPluginExtension)
	}
}

class AndroidStubPluginExtension {
	def compileSdkVersion = 19
}
